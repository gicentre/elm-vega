# Creating Your First Vega Visualization with Elm-Vega

## 1. One-time setup of Elm

If you already have [Elm](http://elm-lang.org) installed, you can skip this section.
If not, you can install Elm by following [these instructions](https://guide.elm-lang.org/install.html).
While not strictly necessary, I would strongly recommend configuring your favourite text editor to be able to format, syntax highlight and lint Elm code (details in the installation guide above).
My preferred editor that accepts Elm-specific plug-ins is [Atom](https://atom.io), but any text editor will do.

## 2. Create HTML/JavaScript page to link with Elm

The easiest way to get Elm-Vega to produce graphical output is to write a skeleton Elm program that sends some Vega to an HTML page.
That HTML page will contain some JavaScript that turns a Vega specification into graphics.

To get started, copy this example to a file `helloWorld.html` somewhere on your machine:

```html
<!DOCTYPE html>
<html>
  <head>
    <title>Hello Vega</title>
    <meta charset="utf-8" />

    <!-- These scripts link to the Vega runtime -->
    <script src="https://cdn.jsdelivr.net/npm/vega@5"></script>
    <script src="https://cdn.jsdelivr.net/npm/vega-embed@5"></script>

    <!-- This is the script generated from Elm -->
    <script src="js/helloWorld.js"></script>
  </head>

  <body>
    <!-- The id indicates the container in which to embed the Vega output. -->
    <div id="helloWorld"></div>

    <script>
      // This function grabs the specifications from Elm (here called namedSpecs).
      // Replace 'HelloWorld' in line below with name of your Elm module when you write your own.
      Elm.HelloWorld.init().ports.elmToJS.subscribe(function(namedSpecs) {
        for (let name of Object.keys(namedSpecs)) {
          vegaEmbed(`#${name}`, namedSpecs[name], { actions: false }).catch(
            console.warn
          );
        }
      });
    </script>
  </body>
</html>
```

Most of this is scaffolding that you won't need to change as you write new Elm-vega applications.
The key elements are:

- Links to `vega` and `vega-embed` JavaScript that do the hard work of converting the Vega specification you will be generating into graphical output.

- A link to the JavaScript file generated by Elm-Vega that creates your Vega specification (here called `helloWorld.js`)

- A `<div>` container with an ID (here called `helloWorld`) that will be the location in the HTML page that will contain your visualization.

- An _Elm subscription_ that links this page with the Elm program that generates the Vega specification.
  Here `Elm.HelloWorld.init()` references an Elm module that must be called `HelloWorld`.
  When you create Elm-Vega modules with different names, you must match that name here (for example, `Elm.MyNewModule.init()`).

## 3. Create an Elm-Vega Program

Create a file called `HelloWorld.elm` in the same location as `helloWorld.html`.
Copy the following code into it.

```elm
port module HelloWorld exposing (elmToJS)

import Platform
import Vega exposing (..)


helloWorld : Spec
helloWorld =
    let
        ds =
            dataSource
                [ dataFromRows "myData"
                    []
                    (dataRow [ ( "label", vStr "Hello from Vega" ) ] [])
                ]

        mk =
            marks
                << mark text
                    [ mFrom [ srData (str "myData") ]
                    , mEncode [ enEnter [ maText [ vField (field "label") ] ] ]
                    ]
    in
    toVega [ ds, mk [] ]



{- This list comprises the specifications to be provided to the Vega runtime.
   In this example, only a single spec 'helloWord' is provided.
-}


mySpecs : Spec
mySpecs =
    combineSpecs [ ( "helloWorld", helloWorld ) ]



{- ---------------------------------------------------------------------------
   The code below is boilerplate for creating a headless Elm module that opens
   an outgoing port to JavaScript and sends the Vega specs (mySpecs) to it.
   There should be no need to change this.
-}


main : Program () Spec msg
main =
    Platform.worker
        { init = always ( mySpecs, elmToJS mySpecs )
        , update = \_ model -> ( model, Cmd.none )
        , subscriptions = always Sub.none
        }


port elmToJS : Spec -> Cmd msg
```

This creates a working Elm-program whose job is to send a Vega specification as a JSON file via an Elm _port_ to the JavaScript seen in the HTML file above.
For convenience, we will always call this port `elmToJS` so it need not be changed when creating new programs.
The only code you need to modify as you write your own Elm-Vega is the function that generates a `Spec`, here called `helloWorld`, and the name of the module, here called `HelloWorld`.
You can add multiple Vega specifications to the same page by creating new `Spec` functions and adding their names to `combineSpecs` where each tuple comprises the name of the spec that can be referenced by the HTML and its corresponding elm function that generates the spec.
Typically, to avoid confusion, both will share the same name (`( "helloWord", helloWorld )` in this example).

This Vega example creates a simple data source called `myData` comprising a single row (`"label" : "Hello from Vega"`) and a single text mark that displays the label defined in the data source.

## 4. Compile the Elm-Vega into JavaScript

The final task is to convert the Elm file into JavaScript so it can be used by the HTML.
To do this, open a command line window (e.g. a Terminal on MacOS or PowerShell on Windows), change to the directory containing your HTML and Elm files and type

    elm make helloWorld.elm --output=js/helloWorld.js --optimize

This should create the `helloWorld.js` file required by the HTML.

Opening `helloWorld.html` in the browser should display a page showing the text "Hello from Vega".

_Note that in this example the data used in the visualization is generated entirely inline and does not rely on external files.
Typical Elm-Vega programs will however load data from external .json, .csv or other files and so normal JavaScript file loading restrictions apply.
As such you would more typically test your applications with a local web server (such as generated by `python -m http.server` or `elm-reactor`) rather than load directly from your file system._

## 5. Next Steps

Most of the setup described above varies little between applications.
To understand more about how Elm-Vega can generate Vega specifications, see

- [creating a simple bar chart](../barChart/README.md),
- the [simple Vega examples](../../vExamples) included in the `vExamples` folder,
- the [vTest-gallery](../../vTest-gallery) for a full range of examples.
